// Note that this is not breakout, just Pong
program

// COLLISION DETECTION
function collide(ball, block) {
    // Rule out cases where ball is not within area
    if ball.x          < block.x then { return 0 }
    if block.x+block.w < ball.x  then { return 0 }
    if ball.y          < block.y then { return 0 }
    if block.y+block.h < ball.y  then { return 0 }

    // Otherwise, make the ball bounce
    if ball.vy<0 then { ball.vy := 2 }
    else              { ball.vy := 0-2 }
    return 1
}

// MAIN BLOCK
{
    // Create structures for game objects
    block := Object(x,y,w,h)
    paddle := new block
    ball := Object(x,y,vx,vy) 
 
    // Initialize positions, velocity, size of game objects
    ball.x := 80    ball.y := 50    ball.vx := 0-1    ball.vy := 0-1   
    block.x := 30   block.y := 20   block.w := 30     block.h := 2
    paddle.x := 30  paddle.y := 80  paddle.w := 30    paddle.h := 2

    // Create structure to store score
    points := Object(player,ai)
    points.player := 0
    points.ai := 0    

    // Switch to determine whether or not AI moves (used to limit speed)
    ai := 0

    // Game loop
    while (1) {        
        // Draw ball & paddles
        paint(ball.x-1,ball.y-1,2,2)
        paint(block.x, block.y, block.w, block.h)
        paint(paddle.x,paddle.y,paddle.w,paddle.h)
  
        // Draw scores
        i:=0 j:=0
        while (i<points.player) { paint(5+j,95,1,1)  j:=j+2 i:=i+1 }
        i:=0 j:=0
        while (i<points.ai)     { paint(5+j, 5,1,1)  j:=j+2 i:=i+1 }

        // Move the ball
        ball.x := ball.x + ball.vx
        ball.y := ball.y + ball.vy

        // Bounce off "walls"
        if ball.x < 5 then { ball.vx :=  1 }
        elsif 100-ball.x < 5 then { ball.vx := 0-1 }

        // Handle goals
        if ball.y<0 then {
            points.player := points.player + 1
            ball.x:=50     ball.y := 75
        }
        if 100<ball.y then {
            points.ai := points.ai + 1
            ball.x:=50     ball.y := 25
        }

        // Handle collision with paddles        
        collide(ball,block)
        collide(ball,paddle)

        // Poll joystick
        paddle.x := paddle.x + poll()

        // Handle AI (skip every third cycle)
        if ai<2 then {
             if ball.x<block.x+15 then { block.x := block.x - 1 }
             if block.x+block.w-15<ball.x then { block.x := block.x + 1 }
             ai := ai + 1
        } else { ai := 0 }

        // Update the screen, then wait 50 ms
        swap()
        wait(50)
    }
}